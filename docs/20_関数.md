
# 関数: [Functions](https://docs.julialang.org/en/v1/manual/functions/)

## 関数の記述
プログラムミングにおける関数 (function) とは，与えられた引数 (argument)をもとに
一連の処理を実行し，結果を値として返却するコードブロックのことと考えておけばよい．

Julia言語では関数は `function ... end` で定義する．

例として，２つの引数 `x`, `y` に対して，和 `x+y` を返却する関数を定義する．

```@repl
function f(x,y)
    return x + y     # 単に x + y でも同じ動作
end

@show f(1,2)  # 関数の呼び出し
```

これは，数学の関数と同様に形式で書くこともできる．
```@repl
g(x,y) =  x + y
@show g(1,2)
```

次のように複数の処理を含む関数は，`function ... end` を用いたほうがわかりやすい．
```@repl
function h(x,y)
    z = x + y
    println("$x + $y = $z")    # 文字列中で`$x`と書くと，変数`x`に置き換わる． 
end
@show h(1,2)
```

引数や返り値（返却値）は必ずしも必要ない．

```@repl
function sayhello()
    println("Hello!")    
end
```
Fortranのように，このような関数をサブルーチンと読んで区別するプログラミング言語もあるが，
大抵の言語では両者は区別されない．

## 合成関数
引数として渡された２つの関数$f,g$の合成関数は
次のように書くことができる．
```@repl
  h(x) = g(f(x))
```

!!! note 
    Juliaでは関数も変数と同じように引数として渡したり，返却したりできる．


## 戻り値の指定 `return`
関数の戻り値は `return` で明示できる． `return` に到達した時点で関数から抜ける． 

```@repl
function mult(x,y)
    println("ここは実行される") 
    return x*y
    println("return後のコードは実行されない")  
end
mult(2,3)
```

単に`return`と書くと，`nothing`が返却される．
```@example
function test()
    println("ここは実行される") 
    return
    println("return後のコードは実行されない") 
end
@show test()
```

## オプション引数 (optional argument)
引数において，`引数名=値`と記述するとデフォルト値を指定できる．

```@repl
F(x, y=100) = x + y
F(1,2)
F(1) # = F(1,100)
G(x=1, y) = x+y  # Error! optional argumentは後方のみで利用できる
```

## キーワード引数 (keyword arugment)
引数をセミコロン`;`で区切った場合，`;` 以降は「キーワード引数」というものになる．
キーワード引数の順序は自由であるが，キーワード引数は必ず `引数名=値`の形で指定する必要がある． 
```@repl
H(x; a, b) = a*x + b
H(1,2,3) # これはエラーになる
H(1, a=2, b=3)
H(1, b=3, a=2)
H(a=2, b=3, 1)  
H(a=2, 1, b=3)  
```

!!! danger
    キーワード引数と通常の引数の順番を入れ替えて混ぜることもできますが，
    読みにくくなるので本講義では非推奨とします．


## 数学関数: [Mathematical Functions](https://docs.julialang.org/en/v1/base/math/#Mathematical-Functions)

Juliaでは基本的な数学関数はほとんど標準で実装されている．

### 基本的関数
| 数学関数      |    Julia      |
|:-------------|:--------------|
| $\sqrt{x}$    | `sqrt(x)`    |
| $x$の絶対値    | `abs(x)`      |
| $x$の絶対値の2乗    | `abs2(x)`      |
| $x$の符号    | `sign(x)`      |
| $x \pmod p$    | `mod(x, p)`      |
| $x$を $p$で割った余り    | `rem(x, p)` or `x % p`|
| $x$を $p$で割った商    | `div(x, p)`      |
| ※ 商と余りの両方       | `divrem(x, p)`       |
| 階乗 $n!$     | `factorial(n)`       |
| 二項係数$\begin{pmatrix} p \\ q \end{pmatrix}$     | `binomial(p,q)`     |

!!! note
     - `abs2`はルートの計算がないので，それだけ `abs`より速い．
     - `rem` と`%` は割られる値が負の場合は動作が異なる．

```@repl
abs(-2)
sign(-1.23)
sign(0.0)
mod(100,3)
mod(-100, 3)
mod(3.5, 3)
3.5 % 3  # mod(3.5, 3) と同じ
mod(-3.5, 3)
-3.5 % 3 # mod(-3.5, 3) とは異なるので注意
divrem(3.5, 3)
factorial(5)
binomial(4,2)
```
### 三角関数，指数関数，対数関数

| 数学関数      |    Julia      |
|:-------------|:--------------|
| $\sin(x)$    | `sin(x)`      |
| $\cos(x)$    | `cos(x)`      |
| $\tan(x)$    | `tan(x)`      |
| $\arcsin(x)$    | `asin(x)`      |
| $\arccos(x)$    | `acos(x)`      |
| $\arctan(x)$    | `atan(x)`      |
| $\exp(x)$       | `exp(x)`       |
| $\log_e(x)$     | `log(x)`       |
| $\log_{10}(x)$     | `log10(x)`     |
| $\log_{2}(x)$     | `log2(x)`     |
| $\log_{a}(x)$     | `log(a,x)`     |

!!! note
     - []()$\cot$などの逆数系や，双曲線関数・逆双曲線関数も一通り実装されているがここでは省略する．
     - []()$\sin(\pi x)$や$\cos(\pi x)$ の計算には `sinpi(x)`, `cospi(x)` といった関数が用意されていてこちらのほうが精度や効率の点で優れている（はず）


### 複素数

| 数学関数      |    Julia      |
|:-------------|:--------------|
| 実部 $\mathrm{Re} z$    | `real(z)`      |
| 虚部 $\mathrm{Im} z$    | `imag(z)`      |
| $z$ の絶対値      | `abs(z)`      |
| $\arg z \in (-\pi, \pi]$    | `angle(z)`      |

```@repl
z = 1.0+sqrt(3)*im    # `im`は虚数単位
real(z)
imag(z)
abs(z)
angle(z)/pi
angle(0)/pi
angle(-1)/pi
angle(1im)/pi
angle(-1im)/pi
```
